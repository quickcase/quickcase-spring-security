plugins {
    id 'org.owasp.dependencycheck' version '7.0.2'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

ext.libVersions = [
        junit: '5.8.2',
        spring: '2.6.3',
        mockito: '4.3.1',
]

allprojects {
    group = 'app.quickcase.spring'
    version = '1.1.1'
}

dependencyCheck {
    failBuildOnCVSS = 0
    suppressionFile = 'dependency-check-suppressions.xml'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    dependencies {
        // Spring
        api "org.springframework.boot:spring-boot-starter-security:${libVersions['spring']}"
        api "org.springframework.boot:spring-boot-starter-oauth2-resource-server:${libVersions['spring']}"
        testImplementation "org.springframework.boot:spring-boot-starter-web:${libVersions['spring']}"
        testImplementation "org.springframework.boot:spring-boot-starter-test:${libVersions['spring']}"

        // Jackson
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'

        // JUnit
        testImplementation "org.junit.jupiter:junit-jupiter-api:${libVersions['junit']}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${libVersions['junit']}"

        // Hamcrest
        testImplementation "org.hamcrest:hamcrest:2.2"

        // Mockito
        testImplementation "org.mockito:mockito-core:${libVersions['mockito']}"
        testImplementation "org.mockito:mockito-junit-jupiter:${libVersions['mockito']}"
    }

    test {
        useJUnitPlatform()

        dependsOn cleanTest
        finalizedBy jacocoTestReport

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            html.required = true
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.9
                }
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    url.set("https://github.com/quickcase/spring-oidc.git")
                }
            }
        }

        repositories {
            maven {
                name = "Github"
                url = uri("https://maven.pkg.github.com/quickcase/spring-oidc")
                credentials {
                    username = System.getenv("GH_USERNAME")
                    password = System.getenv("GH_TOKEN")
                }
            }
        }
    }
}
