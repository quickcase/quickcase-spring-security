wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

ext.libVersions = [
        junit: '5.5.2',
        spring: '2.2.1.RELEASE'
]

allprojects {
    group = 'app.quickcase.security'
    version = '0.2.0-rc4'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        // Spring
        api "org.springframework.boot:spring-boot-starter-security:${libVersions['spring']}"
        api "org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:${libVersions['spring']}"

        // Force use of v2.4.0 as spring-security-oauth2-autoconfigure v2.2.1.RELEASE points to v2.3.7 which
        // still includes dependency on very vulnerable and deprecated v1 of jackson-databind
        api "org.springframework.security.oauth:spring-security-oauth2:2.4.0.RELEASE"

        // JUnit
        testImplementation "org.junit.jupiter:junit-jupiter-api:${libVersions['junit']}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${libVersions['junit']}"

        // Hamcrest
        testImplementation "org.hamcrest:hamcrest:2.1"

        // Mockito
        testImplementation 'org.mockito:mockito-core:3.1.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:3.1.0'
    }

    test {
        useJUnitPlatform()

        dependsOn 'cleanTest'

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    url.set("https://github.com/quickcase/quickcase-spring-security.git")
                }
            }
        }

        repositories {
            maven {
                name = "Github"
                url = uri("https://maven.pkg.github.com/quickcase/quickcase-spring-security")
                credentials {
                    username = System.getenv("GITHUB_USERNAME")
                    password = System.getenv("GITHUB_PACKAGE_TOKEN")
                }
            }
        }
    }
}
