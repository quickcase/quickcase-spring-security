plugins {
    id 'org.owasp.dependencycheck' version '5.3.0'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

ext.libVersions = [
        junit: '5.6.2',
        spring: '2.3.1.RELEASE',
        mockito: '3.3.3'
]

allprojects {
    group = 'app.quickcase.security'
    version = '0.4.0'
}

dependencyCheck {
    failBuildOnCVSS = 0
    suppressionFile = 'dependency-check-suppressions.xml'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = 11
    targetCompatibility = 11

    dependencies {
        // Spring
        api "org.springframework.boot:spring-boot-starter-security:${libVersions['spring']}"
        api "org.springframework.boot:spring-boot-starter-oauth2-resource-server:${libVersions['spring']}"

        // Jackson
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'

        // JUnit
        testImplementation "org.junit.jupiter:junit-jupiter-api:${libVersions['junit']}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${libVersions['junit']}"

        // Hamcrest
        testImplementation "org.hamcrest:hamcrest:2.2"

        // Mockito
        testImplementation "org.mockito:mockito-core:${libVersions['mockito']}"
        testImplementation "org.mockito:mockito-junit-jupiter:${libVersions['mockito']}"
    }

    test {
        useJUnitPlatform()

        dependsOn 'cleanTest'

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    url.set("https://github.com/quickcase/quickcase-spring-security.git")
                }
            }
        }

        repositories {
            maven {
                name = "Github"
                url = uri("https://maven.pkg.github.com/quickcase/quickcase-spring-security")
                credentials {
                    username = System.getenv("GITHUB_USERNAME")
                    password = System.getenv("GITHUB_PACKAGE_TOKEN")
                }
            }
        }
    }
}
